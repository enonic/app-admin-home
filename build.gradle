plugins {
    id 'java'
    id 'maven'
    id 'com.enonic.defaults' version '1.0.3'
    id 'com.enonic.xp.app' version '1.0.17'
    id 'com.moowork.node' version '1.1.1'
}

apply from: "$rootDir/gradle/defaults.gradle"

app {
    name = 'com.enonic.xp.app.main'
    group = 'com.enonic.xp'
    displayName = 'Enonic XP Home App'
    systemVersion = "${xpVersion}"
    systemApp = true
}

dependencies {
    compile "com.enonic.xp:admin-api:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include 'com.enonic.lib:lib-cache:1.1.0'
    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
}

node {
    download = true
    version = '8.10.0'
}

task flush( type: Delete, dependsOn: clean ) {
    description = 'Clean the project from built sources and dependencies'
    delete '.xp'
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = [ 'install', '--force' ]
}

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task webpack( type: NodeTask, dependsOn: lint ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build UI resources (js, css, etc).'
    inputs.dir 'src/main'
    outputs.dir "$buildDir/resources/main"
    script = file( './node_modules/webpack/bin/webpack' )
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

jar {
    exclude 'assets/**/*.less'
    // exclude js sources
    def assetsDir = file('build/resources/main')
    println "$assetsDir/assets/js"
    fileTree("$assetsDir/assets/js") {
        exclude '**/bundle.js'
    }.each { f -> exclude assetsDir.toURI().relativize( file( f.path ).toURI() ).toString() }

    dependsOn += webpack
}

jar.outputs.dir "${buildDir}/resources/main"

if ( hasProperty( 'env' ) )
{
    addBuildDependency()
    applyExcludedTasks()
}

def applyExcludedTasks() {
    def libAdminUi = gradle.includedBuild( 'lib-admin-ui' )
    Closure permittedTasks = { it == 'lint' || it == 'test' }
    def excludedTasks = gradle.startParameter.getExcludedTaskNames().findAll( permittedTasks )
    libAdminUi.getLoadedSettings().getStartParameter().setExcludedTaskNames( excludedTasks )
}

def addBuildDependency() {
    def buildTask = gradle.includedBuild( 'lib-admin-ui' ).task( ':build' )
    def unpackDevResources = tasks.findByPath( ":unpackDevResources" )

    build.dependsOn += buildTask
    clean.dependsOn += gradle.includedBuild( 'lib-admin-ui' ).task( ':clean' )
    flush.dependsOn += gradle.includedBuild( 'lib-admin-ui' ).task( ':flush' )
    webpack.dependsOn += unpackDevResources
    unpackDevResources.dependsOn += buildTask
}

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}
